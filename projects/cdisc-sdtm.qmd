---
title: "Study Data Tabulation Model (SDTM) Automation with SAS"
description: "Automation of SDTM-Demographics (DM)/SUPP creation in SAS for CDISC-compliant clinical data using a metadata-driven approach."
author: "Ousmane DIALLO"
date: "2025-08-30"
keywords: "SDTM, CDISC, SAS, clinical data, biostatistics, portfolio"
format:
  html:
    toc: true
    code-fold: true
---

# SDTM-Demographics (DM) Automation with SAS

Automation of SDTM-Demographics (DM) creation in SAS for CDISC-compliant clinical data using a metadata-driven approach.

[View Code on Github](https://github.com/ousmanerabi/02_SDTM)

## Overview {.section-title}

**Goal**: Build SDTM DM (Demographics) and SUPPDM from raw/source data using a metadata-driven approach (empty shells → mapping → derivations → sort/export).

**Why**: Ensure CDISC consistency (identifiers, timing in ISO 8601, roles/keys) and reproducibility in SAS OnDemand.

**Outputs**: TARGET.DM (1 record per subject) and TARGET.SUPPDM (non-standard qualifiers).

::: callout-note
**Context**: Tools: SAS OnDemand 9.4, macros from study notes. Source folders: SOURCE (raw tables), LIB (formats), TARGET (SDTM outputs). Metadata: SDTM_METADATA1.csv.
:::

## Inputs {.section-title}

-   **Metadata CSV**: SDTM_METADATA1.csv (variables, roles, lengths, types, sort keys).
-   **Source Data**:
    -   SOURCE.DEMOGRAPHIC (1 row per subject, e.g., subject, dob, gender, trt, uniqueid)
    -   SOURCE.DOSING (e.g., subject, startdt, enddt)
-   **Formats** (libref LIB): sex_demographic_gender, race_demographic_race, armcd_demographic_trt, \$arm_demographic_trt.
-   **Environment**: SAS OnDemand 9.4; librefs SOURCE, LIB, TARGET.

## 1. Library Setup {.section-title}

```         
/* Common paths & librefs */
%include "/home/u64197545/CDISC/appendix/common.sas";
/* expects:
 libname source "/home/u64197545/CDISC/data";
 libname lib "/home/u64197545/CDISC/formats";
 libname target "/home/u64197545/CDISC/sdtm";
*/
```

## 2. Create EMPTY DM from Metadata {.section-title}

Create an empty shell (EMPTY_DM) using the CSV metadata to ensure correct attributes (type, length, label, order).

```         
/* Create empty based on metadata CSV */
%macro make_empty_dataset(metadatafile=, dataset=);
/* Import csv file and rename it _temp */
    proc import datafile="&metadatafile" out=_temp dbms=csv replace;
        guessingrows=max;
        delimiter=";";
        encoding='utf-8';
    run;

    /* Sort the dataset by expected specified variable order (varnum) */
    proc sort data=_temp;
        where domain="&dataset";
        by varnum;
    run;

    /* Create keepstring macro variable and load metadata information into macro variables */
    %global &dataset.KEEPSTRING;

    data _null_;
        set _temp nobs=nobs end=eof;
        if _n_=1 then
            call symput("vars", compress(put(nobs, 3.)));
        call symputx('var'    || compress(put(_n_, 3.)), variable);
        call symputx('label'  || compress(put(_n_, 3.)), label);
        call symputx('length' || compress(put(_n_, 3.)), put(length, 3.));
        /* Valid ODM types include TEXT, INTEGER, FLOAT, DATETIME, DATE, TIME and map to SAS numeric or character */
        if upcase(type) in ("INTEGER", "FLOAT") then
            call symputx('type' || compress(put(_n_, 3.)), "");
        else if upcase(type) in ("TEXT", "DATE", "DATETIME", "TIME") then
            call symputx('type' || compress(put(_n_, 3.)), "$");
        else
            put "ERR" "OR: not using a valid ODM type.  " type=;
        /* Create KEEPSTRING macro variable */
        length keepstring $ 32767;
        retain keepstring;
        keepstring=compress(keepstring) || "|" || left(variable);
        if eof then
            call symputx(upcase(compress("&dataset" || 'KEEPSTRING')), 
                left(trim(translate(keepstring, " ", "|"))));
    run;

    /* Create a 0-observation template data set used for assigning variable attributes */
    data EMPTY_&dataset;
        %do i=1 %to &vars;
            attrib &&var&i label="&&label&i" 
            %if "&&length&i" ne "" %then
                length=&&type&i.&&length&i...;
            ;
            %if &&type&i=$ %then
                retain &&var&i '';
            %else
                retain &&var&i .;
            ;
        %end;
        if 0;
    run;
%mend make_empty_dataset;

/* Result: WORK.EMPTY_DM with 0 obs, all expected variables/attributes */
```

## 3. Mapping (Raw Data to SDTM DM) {.section-title}

Mapping raw data to SDTM DM variables using SDTMIG v3.4.

| Class | Variables (examples) | Notes |
|------------------------|------------------------|------------------------|
| Identifier | STUDYID, DOMAIN, USUBJID, SUBJID | 1 record per subject |
| Timing | RFSTDTC, RFENDTC, RFXSTDTC, RFXENDTC, BRTHDTC | ISO 8601 dates |
| Qualifier | SITEID, SEX, RACE, COUNTRY, AGE, AGEU | Coded via formats where applicable |
| Arm | ARMCD, ARM, ACTARMCD, ACTARM | From treatment assignments |

## 4. Create Formats {.section-title}

```         
/* Creating custom format for the variables sex, race, arm, and armcd */
proc format;
    value sex 1='M' 2='F' .='U';
    value race 1='White' 2='Black or African American' 3='Asian';
    value armcd 0='PLACEBO' 1='ALG123';
    value arm 0='Placebo' 1='Analgezia HCL 30mg';
run;
```

## 5. Derive Logic Variables (Dosing) {.section-title}

```         
/* First/last dose (for RFSTDTC/RFENDTC) */
proc sort 
  data=source.dosing(keep=subject startdt enddt) 
  out=dosing;
   by subject startdt;
run;

data dosing;
  set dosing; 
    by subject;
    retain firstdose lastdose;
  if first.subject then 
    do; 
       firstdose=.; 
       lastdose=.; 
    end;
  firstdose = min(firstdose, startdt, enddt);
  lastdose  = max(lastdose , startdt, enddt);
  if last.subject;
run;
```

## 6. Combine All Data {.section-title}

```         
/* Get demographics data */
proc sort
  data=source.demographic
  out=demographic;
    by subject;
run;

/* Merge demographics and first dose date */
data demog_dose;
  merge demographic
        dosing;
    by subject;
run;

/* Derive the majority of SDTM DM variables */
options missing = ' ';
data dm;
  set EMPTY_DM
    demog_dose(rename=(race=_race));
    studyid = 'XYZ123';
    domain = 'DM';
    usubjid = left(uniqueid);
    subjid = put(subject,3.); 
    rfstdtc = put(firstdose,yymmdd10.);  
    rfendtc = put(lastdose,yymmdd10.); 
    rfxstdtc = put(firstdose,yymmdd10.);  
    rfxendtc = put(lastdose,yymmdd10.);
    rficdtc = put(icdate,yymmdd10.);
    rfpendtc = put(lastdoc,yymmdd10.);
    dthfl = 'N';
    siteid = substr(subjid,1,1) || "00";
    brthdtc = put(dob,yymmdd10.);
    age = floor ((intck('month',dob,firstdose) - 
          (day(firstdose) < day(dob))) / 12);
    if age ne . then
      ageu = 'YEARS';
    sex = put(gender,sex.);
    race = put(_race,race.);
    armcd = put(trt,armcd.);
    arm = put(trt,arm.);
    actarmcd = put(trt,armcd.);
    actarm = put(trt,arm.);
    country = "USA"; 
run;
```

## 7. Order, Keys & Export {.section-title}

```         
/* Create sort order */
%macro make_sort_order(metadatafile=, dataset=);
    proc import datafile="&metadatafile" out=_temp dbms=csv replace;
        guessingrows=max;
        delimiter=";";
        encoding='utf-8';
    run;

    proc sort data=_temp;
        where keysequence ne . and domain="&dataset";
        by keysequence;
    run;

    %global &dataset.SORTSTRING;
    data _null_;
        set _temp end=eof;
        length domainkeys $ 200;
        retain domainkeys '';
        domainkeys=trim(domainkeys) || ' ' || trim(put(variable, 8.));
        if eof then
            call symputx(compress("&dataset" || "SORTSTRING"), domainkeys);
    run;
%mend make_sort_order;

/* Sort DM according to metadata and save permanent dataset */
%make_sort_order(metadatafile=/home/u64197545/CDISC/Chap2/SDTM_METADATA1.csv,dataset=DM);
proc sort
  data=dm(keep = &DMKEEPSTRING)
  out=target.dm;
    by &DMSORTSTRING;
run;
```

## 8. QC & Validation {.section-title}

-   **Structural checks**: Variable names, type, length, labels match metadata.
-   **Controlled terminology**: Values mapped via formats/codelists.
-   **Timing**: ISO 8601, completeness of RFSTDTC/RFXSTDTC.
-   **Keys**: 1 record per subject; USUBJID uniqueness.
-   **Cross-domain**: Consistency with EX/AE/VS if applicable.

```         
/* 1 record per USUBJID? */
proc sql;
  select usubjid, count(*) as n
  from target.dm
  group by usubjid
  having calculated n > 1;
quit;

/* Required fields not missing? */
proc freq data=target.dm;
  tables studyid*usubjid*domain / missing list;
run;
```

## 9. Outputs {.section-title}

**SDTM-DM**: Datasets DM.sas7bdat and SUPPDM.sas7bdat compliant with CDISC standards.

**Sample DM Dataset**:

| USUBJID | STUDYID | SEX | AGE | RACE  | ARMCD   |
|---------|---------|-----|-----|-------|---------|
| 001     | XYZ123  | M   | 45  | White | PLACEBO |
| 002     | XYZ123  | F   | 32  | Asian | ALG123  |

**Proc Contents**: Structure and attributes of the final dataset.

::: callout-note
**Note**: The sample dataset above is an example. Actual data can be visualized or tabulated for further illustration.
:::

## Contact {.contact-section}

Interested in this project or my expertise in biostatistics and CDISC? Contact me!

Send an EmailLinkedIn
